/**
 * The Hack Central Processing unit (CPU).
 * Parses the binary code in the instruction input and executes it according to the
 * Hack machine language specification. In the case of a C-instruction, computes the
 * function specified by the instruction. If the instruction specifies to read a memory
 * value, the inM input is expected to contain this value. If the instruction specifies
 * to write a value to the memory, sets the outM output to this value, sets the addressM
 * output to the target address, and asserts the writeM output (when writeM = 0, any
 * value may appear in outM).
 * If the reset input is 0, computes the address of the next instruction and sets the
 * pc output to that value. If the reset input is 1, sets pc to 0.
 * Note: The outM and writeM outputs are combinational: they are affected by the
 * instruction's execution during the current cycle. The addressM and pc outputs are
 * clocked: although they are affected by the instruction's execution, they commit to
 * their new values only in the next cycle.
 */
 
CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    //Check to see if it is an a-instruction or c-instruction
    //A if 0
    //C if 1
    Not(in= instruction[15], out= isAinstruction); //isAinstruction can either be 0 or 1

    //Initial Mux
    Mux16(a= instruction, b= aluoutput, sel= instruction[15], out= out1);

    //A register
    //only load A if (isAinstruction is 1 OR (isAinstruction is 0 and dest bit 1 is 1))
    //So this basically creates the load of the register - does not change anything else
    And(a= instruction[15], b= instruction[5], out= o);
    Or(a= o, b= isAinstruction, out= loadA);

    ARegister(in= out1, load= loadA, out= outA, out[0..14]= addressM);
    
    //D register
    //only load if it is a C-instruction and the dest bit 2 is 1. 
    And(a= instruction[15], b= instruction[4], out= loadD);
    DRegister(in = aluoutput, load= loadD, out= outD);
    
    //Second Mux
    Mux16(a= outA, b= inM, sel= instruction[12], out= out2);

    //ALU
    ALU(x= outD, y= out2, zx= instruction[11], nx= instruction[10], zy= instruction[9], ny= instruction[8], f= instruction[7], no= instruction[6], out= aluoutput, out= outM, zr= isZero, ng= isNeg);

    //WriteM
    //we only write when the dest 3 bit is 1 and it is a c-instruction (only time we write to it) (A is for reading)
    And(a= instruction[15], b= instruction[3], out= writeM);

    // Jump conditions depend on comparison of ALU output (zr, ng) and Jump bits (j1, j2, j3)
    // j1 = instruction[2] (Jump if < 0)
    // j2 = instruction[1] (Jump if == 0)
    // j3 = instruction[0] (Jump if > 0)

    //find positive
    Not(in= isNeg, out= notNeg);
    Not(in= isZero, out= notZero);
    And(a= notNeg, b= notZero, out= isPos); //positive

    //Now we check the rest of the stuff using the assembly language key words
    And(a= instruction[2], b= isNeg, out= JLT); //jump bit 2 is 1 and output is neg 
    And(a= instruction[1], b= isZero, out= JEQ);
    And(a= instruction[0], b= isPos, out= JGT);
    Or(a= JLT, b= JEQ, out= JLE); // <= 0
    Or(a= JLE, b= JGT, out= cond); // something is always 1 which ensures there will be a jump (110 OR 001) - 1
   
    //Load for the pc - only when it is a c-instruction and 
    And(a= cond, b= instruction[15], out= loadPC);

    //PC
    PC(in= outA, load= loadPC, inc= true, reset= reset, out[0..14]= pc);






}